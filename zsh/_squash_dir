#compdef squash_dir
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S -A '-*' : \
'-s[squash always\: create temporarily $DIR_CHANGE/.do-save]' \
'-n[no squashing\:  create temporarily $DIR_CHANGE/.no-save]' \
'-k[kill changes\:  create temporarily $DIR_CHANGE/.No-save]' \
'-f[force restart; if combined with -s or -n create files anyway]' \
'-E[do not stop at first error]' \
'*-T+[Set title option OPT]:title option:_title_opt' \
'-a[alphabetic order]' \
'-e[cancel previous -E]' \
'-S[cancel previous -s]' \
'-N[cancel previous -n]' \
'-F[cancel previous -f]' \
'-K[cancel previous -k]' \
'-A[cancel previous -a]' \
'(1 * -)-h[help]' \
'1:cmds:->cmds' \
'*:names:compadd -- /etc/init.d/squash_*(@:t:s/squash_/)'
local ret=$?
case $state in
(cmds)
	local cmds
	cmds=(
	'status:show status'
	'show:show status'
	'list:show status'
	'info:show status'
	'print:show status'
	'restart:restart squash_dir'
	'start:start squash_dir'
	'stop:stop squash_dir'
	'STOP:execute stop action without stopping'
	'START:execute start action without starting'
	'STATUS:execute status action also for non-running scripts'
	'zap:zap squash_dir to stopped state'
	)
	_describe -t commands 'command' cmds && ret=0;;
esac
return ret
